OBTW
This code was interpreted using loljs:
http://asgaard.co.uk/misc/loljs/#
TLDR
HAI 1.3
  CAN HAS STDIO? 
  BTW read in the nucleotides from stdin
  I HAS A nt_string
  GIMMEH nt_string
  nt_string IS NOW A YARN
  BTW initialize variables
  I HAS A a ITZ 0
  I HAS A c ITZ 0
  I HAS A g ITZ 0
  I HAS A t ITZ 0
  a IS NOW A NUMBR
  c IS NOW A NUMBR
  g IS NOW A NUMBR
  t IS NOW A NUMBR
  I HAS A i ITZ 0
  i IS NOW A NUMBR
  IM IN YR nt_string_loop UPPIN YR i TIL BOTH SAEM i AN LEN OF nt_string
    I HAS A char ITZ nt_string!i
    char, WTF?
      OMG "A"
      OMG "a"
        a R SUM OF a AN 1
        GTFO
      OMG "C"
      OMG "c"
        c R SUM OF c AN 1
        GTFO
      OMG "G"
      OMG "g"
        g R SUM OF g AN 1
        GTFO
      OMG "T"
      OMG "t"
        t R SUM OF t AN 1
        GTFO
      OMGWTF
        GTFO
    OIC
  IM OUTTA YR nt_string_loop
  VISIBLE SMOOSH a " " c " " g " " t MKAY
KTHXBYE 


