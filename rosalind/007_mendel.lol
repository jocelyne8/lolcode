OBTW
This code was interpreted using loljs:
http://asgaard.co.uk/misc/loljs/#
TLDR
HAI 1.3

  BTW I haz a funksion
  HOW DUZ I factorial YR n
    BOTH SAEM 0 AN n
    O RLY?
      YA RLY
        FOUND YR 1
      NO WAI
        FOUND YR PRODUKT OF n AN factorial DIFF OF n AN 1
    OIC
  IF U SAY SO
  
  BTW I haz anothre funksion
  HOW DUZ I mendel YR k AN YR m AN YR n
    I HAS A tot ITZ SUM OF k AN SUM OF m AN n
    
    BTW choose(tot, 2)
    I HAS A C_tot_2, C_tot_2 IS NOW A NUMBAR
    C_tot_2 R QUOSHUNT OF factorial tot AN factorial DIFF OF tot AN 2
    C_tot_2 R QUOSHUNT OF C_tot_2 AN 2
    
    BTW choose(m, 2)
    I HAS A C_m_2, C_m_2 IS NOW A NUMBAR
    C_m_2 R QUOSHUNT OF factorial m AN factorial DIFF OF m AN 2
    C_m_2 R QUOSHUNT OF C_m_2 AN 2
    
    BTW choose(n, 2)
    I HAS A C_n_2, C_n_2 IS NOW A NUMBAR
    C_n_2 R QUOSHUNT OF factorial n AN factorial DIFF OF n AN 2
    C_n_2 R QUOSHUNT OF C_n_2 AN 2
    
    
    BTW double recessive with double recessive
    I HAS A p_rr ITZ 0, p_rr IS NOW A NUMBAR
    p_rr R QUOSHUNT OF C_n_2 AN C_tot_2
    
    BTW double recessive with het
    I HAS A p_rh ITZ 0, p_rh IS NOW A NUMBAR
    p_rh R PRODUKT OF n AN PRODUKT OF m AN 0.5 
    p_rh R QUOSHUNT OF p_rh AN C_tot_2
    
    BTW het with het
    I HAS A p_hh ITZ 0, p_hh IS NOW A NUMBAR
    p_hh R PRODUKT OF 0.25 AN QUOSHUNT OF C_m_2 AN C_tot_2

    I HAS A p ITZ 0, p IS NOW A NUMBAR
    p R SUM OF p_rr AN SUM OF p_rh AN p_hh
    p R DIFF OF 1.0 AN p
    
    FOUND YR p
  IF U SAY SO
  
  
  CAN HAS STDIO? 
  I HAS A k, I HAS A m, I HAS A n
  GIMMEH k, GIMMEH m, GIMMEH n
  k IS NOW A NUMBR, m IS NOW A NUMBR, n IS NOW A NUMBR
  VISIBLE mendel k AN m AN n

KTHXBYE 


